plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'maven-publish'
	id 'com.diffplug.spotless' version '5.8.2'
	id 'io.github.juuxel.loom-quiltflower-mini' version '1.1.0'
	id 'pmd'
}

////////////////

version = "0.9.3"
group = "agency.highlysuspect.incorporeal" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "incorporeal"

////////////////

loom {
	runs {
		create("GameTest") {
			server()
			vmArg "-Dfabric-api.gametest=1"
		}
	}
}

sourceSets.main.resources {
	srcDir 'src/generated/resources'
}

repositories {
	maven {
		name = "Modmuss"
		url = "https://maven.modmuss50.me/"
	}
	maven {
		name = "Jared"
		url = "https://maven.blamejared.com/"
	}
	maven {
		url = "https://maven.terraformersmc.com/" // for trinkets
	}
	maven {
		name = "Ladysnake Libs"
		url = 'https://ladysnake.jfrog.io/artifactory/mods'
	}
	maven {
		name = "BuildCraft" // for LBA
		url = "https://mod-buildcraft.com/maven"
	}
	maven {
		name "entity reach"
		url "https://maven.jamieswhiteshirt.com/libs-release/"
	}
	maven {
		url "https://jitpack.io" // for step height eam
	}
	maven {
		url "https://maven.shedaniel.me/" // for rei
	}
	maven {
		url "https://server.bbkr.space/artifactory/libs-release/" // for bannerplusplus
	}
	maven {
		name = "ParchmentMC"
		url = "https://maven.parchmentmc.net/"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:1.17.1"
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-1.17.1:2021.10.31@zip")
	}
	
	modImplementation "net.fabricmc:fabric-loader:0.11.6"
	modImplementation "net.fabricmc.fabric-api:fabric-api:0.42.1+1.17"
	
	//bota itself
	modImplementation "vazkii.botania:Botania:1.17.1-422-FABRIC-SNAPSHOT"
	
	//shit i copied and pasted from botania fabric's buildscript
	//"include"s have been removed
	//todo what happens if i delete these lol
	modImplementation "vazkii.patchouli:Patchouli:1.17.1-58-FABRIC-SNAPSHOT"
	modImplementation "me.shedaniel:RoughlyEnoughItems-fabric:6.2.333"
	modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:3.0.0"
	modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:3.0.0"
	modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-block:3.0.0"
	modImplementation "dev.emi:trinkets:3.0.4"
	modImplementation "com.jamieswhiteshirt:reach-entity-attributes:2.1.1"
	modImplementation "com.github.emilyploszaj:step-height-entity-attribute:v1.0.1"
	modImplementation "io.github.fablabsmc:bannerpp:2.0.4+mc.1.17-rc1"
}

processResources {
	inputs.property "version", project.version
	
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
	
	// Exclude datagenerator .cache directory
	exclude '.cache'
	
	// Exclude Forge-specific metadata on Fabric only
	exclude 'META-INF/mods.toml'
	exclude 'META-INF/accesstransformer.cfg'
	
//	filesMatching('data/botania/patchouli_books/lexicon/book.json') {
//		filter {
//			it.replaceAll("buildNumber", config.build_number)
//		}
//	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

java {
	withSourcesJar()
}
jar {
	from "LICENSE"
	
	exclude "**/*.bat"
	exclude "**/*.psd"
	exclude "**/*.exe"
}